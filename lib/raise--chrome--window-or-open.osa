#!/usr/bin/osascript
# vim:tw=0:ts=2:sw=2:et:norl:ft=applescript
# Author: Landon Bouma <https://tallybark.com/>
# Project: https://github.com/DepoXy/macOS-skhibidirc#ðŸ‘¤
# License: MIT                                        ðŸš½

# Copyright (c) Â© 2024 Landon Bouma. All Rights Reserved.

# USAGE: Open first Chrome window found with specified title;
# if no window found, open new window using specified URL.
#
# - E.g.,
#
#   osascript ~/.kit/mOS/macOS-skhibidirc/lib/raise--chrome--window-or-open.app \
#     "https://www.basecampfitness.com/schedule" \
#     "Basecamp Fitness "
#
# You can specify multiple window titles to match first one:
#
#   osascript ~/.kit/mOS/macOS-skhibidirc/lib/raise--chrome--window-or-open.app \
#     "https://mail.google.com/mail/u/0/#inbox" \
#     "Inbox " \
#     "Google Calendar - " \
#     "Mail - "

# GRIPE/2024-07-05: Doesn't work when window is on another Space,
# it fronts some other Chrome window instead (on the current Space).

# SAVVY/2024-07-08: Cannot execute AppleScript files, even with
# proper `#!/usr/bin/osascript` shebang and 775 permissions:
#   $ /path/to/script.app
#   Killed: 9

on run argv
  repeat with argc from 2 to length of argv
    set match_title to item argc of argv
    tell application "Google Chrome"
      repeat with w in windows
        tell w
          if title of w starts with match_title then
            set index to 1

            tell application "System Events" to tell process "Google Chrome"
              perform action "AXRaise" of window 1
              set frontmost to true
            end tell

            return
          end if
        end tell
      end repeat
    end tell
  end repeat

  # Call the DepoXy opener, which includes --new-window.
  # - ALTLY: If you don't want to use sensible-open, you
  #          could replace this line with open-location:
  #     open location (item 1 of argv)
  if (item 1 of argv) is not equal to "" then
    do shell script "${SHOILERPLATE:-${HOME}/.kit/sh}/sh-sensible-open/bin/sensible-open " & (item 1 of argv)
  end if
end run

