# vim:tw=0:ts=2:sw=2:et:norl:ft=applescript
# Author: Landon Bouma <https://tallybark.com/>
# Project: https://github.com/DepoXy/macOS-skhibidirc#ðŸ‘¤
# License: MIT                                        ðŸš½

# SAVVY: Alacritty is not scriptable.
#
# - I.e., cannot `tell application "Alacritty"`, so tell "System Events".
#
# - CXREF: See comment in a related project for more notes:
# 
#     https://github.com/landonb/home-fries/blob/release/lib/term/window-title--alacritty-number.osa
#       ~/.kit/sh/home-fries/lib/term/window-title--alacritty-number.osa

on run argv
  tell application "Alacritty"
    activate
  end tell

  tell application "System Events"
    -- Get the frontmost app's *process* object.
    set frontAppProcess to first application process whose frontmost is true
  end tell

  # Tell the *process* to iterate its windows.
  tell application "System Events"
    tell frontAppProcess
      repeat with w in windows
        tell w
          -- E.g.,
          --   if NAME of w starts with "2. " then
          -- Doesn't work:
          --   if NAME of w starts with quoted form of (item 1 of argv) & ". " then
          if NAME of w starts with (item 1 of argv) & ". " then
            tell application "System Events"
              tell frontAppProcess
                perform action "AXRaise" of w
                set frontmost to true
                return
              end tell
            end tell
          end if
        end tell
      end repeat
    end tell
  end tell
end run

